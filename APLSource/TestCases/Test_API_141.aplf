 R←Test_API_141(stopFlag batchFlag);⎕TRAP;tempFolder;buff;rc;msg;qdmx;zipfile
⍝ Runs "Squash" on "main" (bound to fail)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempFolder←FilesAndDirs.GetTempSubDir⊃⎕SI
 zipfile←##.CiderConfig.HOME,'/TestData/TestProject.zip'
 zipfile ZipArchive.UnzipTo tempFolder

 (rc msg buff)←tempFolder ##.APLGit2.U.RunGitCommand'checkout main'
 Assert 0=rc
 Assert'main'≡##.APLGit2.CurrentBranch tempFolder

 :Trap 811
     tempFolder ##.APLGit2.Squash'All my changes'
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'No squashing of main/master'≢qdmx.EM
 :EndTrap

 R←T._OK

∆TidyUp:
 (rc msg)←FilesAndDirs.RmDirByForce tempFolder
 msg Assert rc=0
⍝Done
