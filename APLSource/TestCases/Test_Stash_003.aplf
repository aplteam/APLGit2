 R←Test_Stash_003(stopFlag batchFlag);⎕TRAP;dir;rc;msg;list;buff;parms
⍝ Executes StashPush on a tracked and an untracked file and restore them with StashPop
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 dir←∆StashDir,'/SourceCode'
 'Create!'FilesAndDirs.CheckPath dir

 (⊂'Hello')⎕NPUT dir,'/file_1.txt'
 ##.APLGit2.Add ∆StashDir
 list←##.APLGit2.StashList ∆StashDir
 →T.GoToTidyUp 0<≢list
 (⊂'Hello2')⎕NPUT dir,'/file_2.txt'
 parms←##.APLGit2.CreateStashPushParms
 parms.folder←∆StashDir
 parms.addUntracked←1
 buff←##.APLGit2.StashPush parms
 →T.GoToTidyUp~∨/'Saved working directory and index state'⍷∊buff
 buff←##.APLGit2.StashPop ∆StashDir
 →T.GoToTidyUp 1≠∨/'stash pop stash@{0}'⍷∊buff  ⍝ Untracked files are NOT listed. Not recognized as a bug by the Git team.
 →T.GoToTidyUp'file_1.txt' 'file_2.txt'{⍺≢⍺{((-≢⍺)↑∊⍵)}¨FilesAndDirs.ListFiles ⍵}dir

∆TidyUp:
 (rc msg)←FilesAndDirs.RmDirByForce dir
 msg Assert 0=rc
⍝Done
