 R←Test_API_008(stopFlag batchFlag);⎕TRAP;tempFolder;buff;res;parms;data;expected
⍝ Runs "AddGitIgnore"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If batchFlag
     →0 ⋄ R←T._NoBatchTest
 :EndIf

 C.Pause'This test needs you: when a ⎕ED pops up (twice!) just close it without a change.'

 tempFolder←FilesAndDirs.GetTempSubDir'APLGit2-',⊃⎕SI
 (tempFolder,'/')⎕NCOPY⍠('Wildcard' 1)⊢##.CiderConfig.HOME,'/TestData/CiderProject/*'
 parms←⎕SE.Cider.CreateOpenParms''
 parms.folder←tempFolder
 parms.quietFlag←1
 parms.watch←'ns'
 {}⎕SE.Cider.OpenProject parms

 G.CommTools.YesOrNo_Answers←1 2⍴'Add .gitignore for APLers to ' 'Y'
 G.CommTools.YesOrNo_Answers⍪←'Add special statements for them?' 'N'
 G.CommTools.YesOrNo_Answers⍪←'Would you like to add the destination folder' 'N'
 res←G.AddGitIgnore tempFolder
 →T.GoToTidyUp~∨/'.gitignore created in'⍷res
 →T.GoToTidyUp~⎕NEXISTS tempFolder,'/.gitignore'
 data←⊃FilesAndDirs.NGET(tempFolder,'/.gitignore')1
 expected←'aplcore' 'CONTINUE' 'CONTINUE.dws' 'conga.crash'
 →T.GoToTidyUp expected≢data

 G.CommTools.YesOrNo_Answers[2;]←'Add special statements for them?' 'Y'
 G.CommTools.Select_Choices←1 2⍴'Select desired action on' 2
 res←G.AddGitIgnore tempFolder
 expected←'aplcore' 'CONTINUE' 'CONTINUE.dws' 'conga.crash' '/packages_dev/*' '!/packages_dev/apl-dependencies.txt' '!/packages_dev/apl-buildlist.json'
 data←⊃FilesAndDirs.NGET(tempFolder,'/.gitignore')1
 →T.GoToTidyUp≢/FilesAndDirs.EnforceSlash¨¨expected data

 R←T._OK

∆TidyUp:
 G.CommTools.Cleanup
 {}⎕SE.Cider.CloseProject'#.BooBoo'
 Assert 0=⊃buff←FilesAndDirs.RmDir tempFolder
 #.⎕EX'BooBoo'
⍝Done
