 R←Test_UC_005(stopFlag batchFlag);⎕TRAP;rc;tempDir;stop;msg;buff
⍝ Runs ]Init (initialize a folder for Git)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←FilesAndDirs.GetTempSubDir'APLGit2-Tests'
 (⊂'This' 'and that')⎕NPUT tempDir,'/foo.txt'
 (⊂'r←Hello' 'r←''World''')⎕NPUT tempDir,'/Hello.aplf'
 ⎕SE.APLGit2.##.⎕ex'DEFAULT_PROJECT'

 stop←0
 (rc msg buff)←stop ∆UCMD'Init "',tempDir,'" -quiet'
 msg Assert rc=0
 →T.GoToTidyUp 0<≢msg
 →T.GoToTidyUp≢/FilesAndDirs.NormalizePath¨buff tempDir
 →T.GoToTidyUp~⎕NEXISTS tempDir,'/.git'
 →T.GoToTidyUp 0=⎕NEXISTS tempDir,'/.gitignore'
 →T.GoToTidyUp~⎕NEXISTS tempDir,'/.gitattributes'

 R←T._OK

∆TidyUp:
 Assert 0=⊃buff←FilesAndDirs.RmDir tempDir
⍝Done
