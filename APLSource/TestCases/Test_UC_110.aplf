 R←Test_UC_110(stopFlag batchFlag);⎕TRAP;stop;rc;msg;buff;tempFolder;folder;q;parms
⍝ Attempts to run "]CompareCommits" (uses ]CompareFiles for putting diffs on display)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If batchFlag
     →0,R←T._NoBatchTest
 :EndIf
 :If 0=⎕SE.⎕NC'CompareFiles'
     →0,R←T._NotApplicable
 :EndIf

 q←''
 q,←⊂' This test needs you: it will run the command ]CompareCommits which will use an external'
 q,←⊂' comparison utility via the ]CompareFiles user command'
 q,←⊂' Would you like to run this test?'
 :If 0=1 ∆YesOrNo q
     →0,R←T._NoBatchTest
 :EndIf

 parms←⎕SE.Cider.CreateOpenParms''
 parms.quietFlag←1
 parms.folder←##.CiderConfig.HOME,'/TestData/CommTools'
 Assert ⎕SE.Cider.OpenProject parms

 stop←0
 (rc msg buff)←stop ∆UCMD'CompareCommits -project=#.CommTools'
 msg Assert rc=0
 →T.PassesIf 0=≢msg

 →T.GoToTidyUp~1 ∆YesOrNo'Did a comparison utility show up?'

 R←T._OK

∆TidyUp:
 Assert ⎕SE.Cider.CloseProject'#.CommTools'
 #.⎕EX'CommTools'
⍝Done
