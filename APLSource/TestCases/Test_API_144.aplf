 R←Test_API_144 (stopFlag batchFlag);⎕TRAP;tempFolder;rc;msg;zipfile;qdmx
⍝ Runs "Squash" on "main" with negative confirmation
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempFolder←FilesAndDirs.GetTempSubDir⊃⎕SI
 zipfile←##.CiderConfig.HOME,'/TestData/GitProject.zip'
 zipfile ZipArchive.UnzipTo tempFolder

 G.Add tempFolder
 {}'README polished'G.Commit tempFolder
 Assert'main'≡G.CurrentBranch tempFolder

 :Trap 0
     G.CommTools.YesOrNo_Answers←1 2⍴'SquashOnMain@' 'n'  ⍝ Without confirmation an error must be thrown
     {}tempFolder G.Squash'Squash first two commit' 2
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'Cancelled by user'≢qdmx.EM
 :EndTrap

 R←T._OK

∆TidyUp:
 (rc msg)←FilesAndDirs.RmDirByForce tempFolder
 msg Assert rc=0
⍝Done
