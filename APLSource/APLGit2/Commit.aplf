 r←{msg}Commit y;project;buff;rc;⎕TRAP;cb;path;cmd;msg_;space
⍝ Commit changes.\\
⍝ If the current branch is "main" (or "master") then `msg` is check for carrying a meaningful message.
⍝ It none was passed a confirmation is required.
⍝ `msg` may be empty but will become three dots (`...`).\\
⍝ `y` must be one of:
⍝   * Fully qualfied name of a namespace that is an opened Cider project
⍝   * Fully qualified path to a Git-managed project
⍝ Returns a two-element vector:
⍝  1. The name of the branch
⍝  2. The hash of the commit as a simple text string
 ⎕TRAP←(811 'E' '⎕DMX.EM ⎕signal 811')(0 'N')
 msg←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'msg'
 msg←{0=≢⍵:'...' ⋄ ⍵}msg
 (space path)←EstablishProject y
 cb←CurrentBranch path
 :If (⊂cb)∊'master' 'main'
 :AndIf 0=≢msg~'.'
     'Cancelled by user'U.Assert CommTools.YesOrNo'NoMsgOnCommit@No meaningful message provided on "',cb,'"; continue anyway?'
 :EndIf
 msg_←U.MassageMsg msg
 cmd←'commit -m "',msg_,'"'
 (rc msg buff)←path U.RunGitCommand cmd
 :If ∨/'nothing to commit, working tree clean'⍷∊buff
     r←buff
 :Else
     msg U.Assert 0=rc
     r←buff
 :EndIf
⍝Done
