 Make flag;M;C;path;zipFilename;res;cfg;parms;pkgCfgFilename
⍝ "Make" a new version of this project
 :If flag
     M←⎕SE.MakeHelpers
     C←##.CiderConfig
     path←C.HOME,'/',C.CIDER.distributionFolder
     pkgCfgFilename←C.HOME,'/apl-package.json'
     cfg←⎕JSON⍠('Dialect' 'JSON5')⊢⊃⎕NGET pkgCfgFilename
     M.FetchLaterUserCommand cfg C'[MyUCMDs]'
     parms←⎕SE.Tatin.CreateBuildParms C.HOME
     parms.targetPath←path
     :If ⎕NULL≡cfg←pkgCfgFilename M.EditVersion cfg
         ⍞←'Cancelled by user' ⋄ →0
     :Else
         parms.version←cfg.version
         M.RecreateFolder path
         {}⎕SE.Tatin.CreateAPIfromCFG ##.APLGit2 cfg
         zipFilename←⎕SE.Tatin.BuildPackage parms
         ⎕←'*** New version build successfully:',M.CR,'   ',zipFilename
         :If 0<≢cfg.userCommandScript
         :AndIf 1 M.YesOrNo'Install new version in MyUCMDs?'
             {}M.##.FilesAndDirs.RmDirByForce M.GetMyUCMDsFolder cfg.name
             res←⎕SE.Tatin.InstallPackages zipFilename'[MyUCMDs]'
             ⎕NDELETE⎕SE.SALTUtils.(USERDIR,UCMDCACHEFILE)
             ⎕←'  New version installed as user command in MyUCMDs/: ',res
         :EndIf
     :EndIf
 :EndIf
 ⍝Done
